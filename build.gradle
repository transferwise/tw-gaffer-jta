import org.eclipse.jgit.api.errors.RefAlreadyExistsException

plugins {
    id "idea"
    id 'com.github.spotbugs' version '5.0.3'
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
}

ext.projectName = 'TransferWise Gaffer 1PC JTA'
ext.projectDescription = 'Transferwise Gaffer 1PC JTA - efficient 1PC JTA for enterprise services'
ext.projectGitHubRepoName = 'tw-gaffer-jta'
ext.projectArtifactName = 'tw-gaffer-jta'

apply from: 'build-common.gradle'

dependencies {
    api 'javax.transaction:javax.transaction-api:1.2'
    api 'javax.jms:jms-api:1.1-rev-1'

    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'javax.annotation:jsr250-api:1.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    testImplementation 'org.springframework:spring-tx:4.2.3.RELEASE'
    testImplementation 'org.springframework:spring-test:4.2.3.RELEASE'
    testImplementation 'org.springframework:spring-jdbc:4.2.3.RELEASE'
    testImplementation 'org.springframework:spring-context:4.2.3.RELEASE'
    testImplementation 'org.springframework:spring-jms:4.2.3.RELEASE'
    testImplementation 'org.springframework:spring-orm:4.2.3.RELEASE'
    testImplementation 'org.hibernate:hibernate-core:4.3.11.Final'
    testImplementation 'com.google.guava:guava:30.1.1-jre'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.hsqldb:hsqldb:2.3.0'
    testImplementation 'org.apache.tomcat:tomcat-jdbc:9.0.19'
    testImplementation 'org.apache.activemq:activemq-pool:5.7.0'
    testImplementation 'ch.qos.logback:logback-classic:1.2.9'
    testImplementation 'org.apache.xbean:xbean-spring:3.14'
}

idea.project {
    vcs = 'Git'
    languageLevel = JavaVersion.VERSION_11
    targetBytecodeVersion = JavaVersion.VERSION_11
}

task tagRelease {
    doLast {
        try {
            grgit.tag.add {
                name = "v$version"
                message = "Release of ${version}"
            }
            grgit.push(refsOrSpecs: ["v$version"])
        }
        catch (RefAlreadyExistsException ignored) {
            logger.warn("Tag v$version already exists.")
        }
    }
}

group = "com.transferwise.common"

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}
